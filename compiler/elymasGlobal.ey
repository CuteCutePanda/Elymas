"elymasAsm.ey" include

0 /TOKINT defv
1 /TOKSTR defv
2 /TOKID defv

{ /f deff -101 /s defv regex { f } { s } ? * } /rxparse deff

{ " " cat
  { < /type defv /value defv > } /token deff
  [ -01 { _ "" streq not } {
    0 /matched defv { /f deff matched { -- } { { 1 =matched f } rxparse } ? * } /parse deff

    "^ (.*)" { } parse
    "^#" { "" } parse
    "^(\\d+) +(.*)" { TOKINT token -01 } parse
    "\"(.*)" {
      "" /str defv
      { _ "^\"(.*)" regex { -01 -- 0 } { 1 } ? * } {
        0 /strmatched defv { /f deff strmatched { -- } { { 1 =strmatched f } rxparse } ? * } /strparse deff

        "^\\\\\\\\(.*)" { str "\\" cat =str } strparse
        "^\\\\n(.*)" { str "\n" cat =str } strparse
        "^\\\\\"(.*)" { str "\"" cat =str } strparse
        "^([^\"\\\\])(.*)" { str -01 cat =str } strparse
        strmatched not { "Tokenization of string-like failed" die } rep
      } loop
      str TOKSTR token -01
    } parse
    "^([^a-zA-Z0-9 ]+)([a-zA-Z0-9][^ ]*) +(.*)" { -201 TOKSTR token " " -1203 cat cat } parse
    "^([a-zA-Z0-9]+|[^a-zA-Z0-9 ]+) +(.*)" { TOKID token -01 } parse

    matched not { "Tokenization failed: " -01 cat die } rep
  } loop -- ]
} /tokenize deff

{
  dump
  [ ] assembler .execute
} /interpretToken deff

{ /input defv
  "" {
    4096 input .readstr cat
    _ "" streq not
  } {
    { _ "([^\\n]*)\\n(.*)" regex } { -102 -- tokenize |interpretToken each } loop
  } loop
} /executeFile deff

# vim: syn=elymas
